import static org.gradle.api.JavaVersion.VERSION_11
import static org.gradle.jvm.toolchain.JavaLanguageVersion.of

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
}

java {
    toolchain {
        languageVersion = of(11)
    }
}

configurations {
    testingLibraries {
        canBeConsumed = false
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.slf4j:slf4j-api:$slf4j_version"

    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"

    testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j2-impl:$log4j_version"
    testRuntimeOnly "org.apache.logging.log4j:log4j-core:$log4j_version"

    testingLibraries "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testingLibraries "org.slf4j:slf4j-simple:$slf4j_version"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        languageVersion = '1.7'
        apiVersion = '1.7'
        jvmTarget = VERSION_11
        freeCompilerArgs = [ '-Xjvm-default=all' ]
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    doFirst {
        // Prevent the project from creating temporary files outside of the build directory.
        systemProperty 'java.io.tmpdir', buildDir.absolutePath

        systemProperty 'java.security.policy', file('permissive.policy').toURI().toURL()
        systemProperty 'test.gradle.user.home', gradle.gradleUserHomeDir.toURI().path
        systemProperty 'test.gradle.home', gradle.gradleHomeDir.toURI().path
        systemProperty 'test.project.dir', projectDir.toURI().path
        systemProperty 'testing-libraries.path', configurations.testingLibraries.asPath

        systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'info'
        systemProperty 'org.slf4j.simpleLogger.dateTimeFormat', 'yyyy-MM-dd HH:mm:ss:SSS Z'
        systemProperty 'org.slf4j.simpleLogger.showDateTime', true
        systemProperty 'org.slf4j.simpleLogger.showShortLogName', true
        systemProperty 'org.slf4j.simpleLogger.showThreadName', false
    }
}

wrapper {
    gradleVersion = '7.5.1'
}
